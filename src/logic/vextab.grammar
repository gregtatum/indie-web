// https://lezer.codemirror.net/docs/guide/
// Regenerate with the command:
//
// lezer-generator src/logic/vextab.grammar > src/logic/vextab-generated.ts

@top Program { expression* }

expression {
  TabStave | NotesSection | Options
}

Boolean { kw<"true"> | kw<"false"> }
Number { number }
Float { number | (number "." number) }
Identifier { identifier }

@skip { space }

kw<term> { @specialize[@name={term}]<identifier, term> }

NoteLetter {
  kw<"A"> | kw<"B"> |  kw<"C"> |  kw<"D"> |  kw<"E"> |  kw<"F"> |  kw<"G">
}

Clef {
  kw<"treble"> | kw<"alto"> | kw<"tenor"> | kw<"bass"> | kw<"percussion">
}

Key {
  kw<"C"> | kw<"Am"> | kw<"F"> | kw<"Dm"> | kw<"Bb"> | kw<"Gm"> | kw<"Eb"> | kw<"Cm"> |
  kw<"Ab"> | kw<"Fm"> | kw<"Db"> | kw<"Bbm"> | kw<"Gb"> | kw<"Ebm"> | kw<"Cb"> |
  kw<"Abm"> | kw<"G"> | kw<"Em"> | kw<"D"> | kw<"Bm"> | kw<"A"> | kw<"F#m"> | kw<"E"> |
  kw<"C#m"> | kw<"B"> | kw<"G#m"> | kw<"F#"> | kw<"D#m"> | kw<"C#"> | kw<"A#m">
}

TimeSignature {
  kw<"C"> | kw<"C|"> | (number "/" number)
}

Tuning {
  kw<"standard"> | kw<"dropd"> | kw<"eb"> |
  kw<"E/5"> | kw<"B/4"> | kw<"G/4"> | kw<"D/4"> | kw<"A/3"> | kw<"E/3">
}

trailingSeparator<ch, content> { "" | content (ch content?)* }
separatorNonEmpty<ch, content> { content (ch content)* }

// "tabstave notation=true tablature=false"
TabStave { kw<"tabstave"> (StaveAttributes)* }

StaveAttributeName<name> { kw<name> }
StaveAttributeValue<value> { value }

StaveAttributes {
  (StaveAttributeName<"notation">  "=" StaveAttributeValue<Boolean> ) |
  (StaveAttributeName<"tablature"> "=" StaveAttributeValue<Boolean> ) |
  (StaveAttributeName<"clef">      "=" StaveAttributeValue<Clef> ) |
  (StaveAttributeName<"key">       "=" StaveAttributeValue<Key> ) |
  (StaveAttributeName<"time">      "=" StaveAttributeValue<TimeSignature>) |
  (StaveAttributeName<"tuning">    "=" StaveAttributeValue<Tuning> )
}

// "options width=400"
Options { kw<"options"> (OptionsAttributes)* }

OptionsAttributeName<name> { kw<name> }
OptionsAttributeValue<value> { value }

OptionsAttributes {
  (OptionsAttributeName<"width">          "=" OptionsAttributeValue<Number> ) |
  (OptionsAttributeName<"scale">          "=" OptionsAttributeValue<Float> ) |
  (OptionsAttributeName<"space">          "=" OptionsAttributeValue<Number> ) |
  (OptionsAttributeName<"stave-distance"> "=" OptionsAttributeValue<Number> ) |
  (OptionsAttributeName<"font-face">      "=" OptionsAttributeValue<Identifier> ) |
  (OptionsAttributeName<"font-style">     "=" OptionsAttributeValue<Identifier> ) |
  (OptionsAttributeName<"font-size">      "=" OptionsAttributeValue<Number> )
}

Sharp { "#" }
DoubleFlat { "##" }
Flat { "@" }
DoubleSharp { "@@" }
Natural { kw<"n"> }
NoteSuffix {
  Sharp |
  DoubleFlat |
  Flat |
  DoubleSharp |
  Natural
}

SingleBar { "|" }
DoubleBar { "=||" }
RepeatBegin { "=|:" }
RepeatEnd { "=:|" }
DoubleRepeat { "=::" }
EndBar { "=|=" }
BarLine {
  SingleBar |
  DoubleBar |
  RepeatBegin |
  RepeatEnd |
  DoubleRepeat |
  EndBar
}

Octave { "/" Number }
Bend { kw<"b"> Number }
FretNumber { number (Bend | (Bend Bend))? }
Rest { "##" | ("#" number "#") }
Note { NoteLetter NoteSuffix? }
OctaveGroup {
  separatorNonEmpty<
    "-",
    Note | FretNumber
  >
  Octave
}

Bar {
  (OctaveGroup | Rest)+
}

NotesSection {
  kw<"notes"> trailingSeparator<BarLine, Bar>
}

@tokens {
  identifier { $[a-zA-Z_]+ }
  number { $[0-9]+ }
  space { $[ \t\n\r]+ }
}
